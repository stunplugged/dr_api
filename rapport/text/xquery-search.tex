
\section{Find alle kategorierne til programserierne}
Brugeren af videoapplikationen skal have muligheden for at navigere imellem de forskellige programserieres kategorier, som der findes på et givent tidspunkt. Funktionen i figur \ref{xquerySearch:getProgramSeriesCategory} bruger et XPath udryk til at finde alle kategori navnene i XML dokumentet og funktionen distinct-value() selekterer kun unik navne. En ”for lykke” returnerer kategorinavne som et element, \ele{category}, og derudover returnerer funktionen getProgramSeriesCategory() et element, \ele{countCategory}, med antal af forskellige kategorier. 

\begin{figure}[ht]
%\centering
\begin{lstlisting}[style=FAKE_XQUERY, language=XQUERY]

declare function dr:getProgramSeriesCategory
  ( $programseriesFileName as xs:string) as element()*{
    
    let $progSerieLabels := distinct-values(doc($programseriesFileName)/ArrayOfProgramSerie/ProgramSerie/
		Labels/string/text())
    return (
      <countCategory>{count($progSerieLabels)}</countCategory>,
      (
      for $label in $progSerieLabels
        return <category>{$label}</category>
      )
    )
};

\end{lstlisting}
\caption{Funktion til at finde kategorinavne og samlet antal af kategorier.}
\label{xquerySearch:getProgramSeriesCategory}
\end{figure}


\section{Find antal programserier i en kategori}
Når en bruger vælger en kategori skal videoapplikationen vise antallet af programserier i kategorien og forskellige informationer om disse. I funktionen \ref{xquerySearch:programSeriesOfCategory} finder XPath udtrykket alle \ele{ProgramSerie} elementer, hvor elementet \ele{string} i elementet \ele{Labels} har den sammen værdi som den valgte kategori. Variablen \$progSeries indeholder en sekvens med alle fundende \ele{ProgramSerie} elementer og ikke kun elementet \ele{Labels} fordi /.. ved slutning af sekvensen vælger forældre elementet, som er \ele{ProgramSerie}.  XQuery funktionen count() tæller antallet af fundende \ele{ProgramSerie} elementer. Denne information og alle de fundende \ele{ProgramSerie} elementer bliver returneret i elementet \ele{programSeriesCategory}.

\begin{figure}[ht]
%\centering
\begin{lstlisting}[style=FAKE_XQUERY, language=XQUERY]

declare function dr:programSeriesOfCategory
  ( $programseriesFileName as xs:string, $category as xs:string ) as element()*{
    
    let $progSeries := doc($programseriesFileName)/ArrayOfProgramSerie/
    ProgramSerie/Labels[string=$category]/..
    return (
      <programSeriesCategory>
        <nameCategory>{$category}</nameCategory>
        <countProgSeries>{count($progSeries)}</countProgSeries>
        {
         for $progSerie in $progSeries
           return $progSerie
         }
      </programSeriesCategory>
    )
};

\end{lstlisting}
\caption{Funktion til at finde antallet af programserier i en bestemt kategori.}
\label{xquerySearch:programSeriesOfCategory}
\end{figure}

\section{Find nyeste videoer inden for en specifik kategori}

For at videoapplikationen kan præsentere detaljeret videoinformationer og de nyeste videoer, når en bruger har valgt en kategori, er det nødvendigt at hente information fra XML dokumentet all.xml. Informationerne om de enkelte videoer ligger altså ikke i det sammen XML dokument, hvor informationen om programserierne og kategorier er gemt. I dette tilfælde findes kategori informationer i dokumentet programseries.xml og videoinformationen i dokumentet all.xml. Et unikt video- ID som hedder \ele{NewestVideoId} i dokumentet programseries.xml og \ele{Id} i dokumentet all.xml referere fra et specifikt \ele{programserie} element til et specifikt \ele{ProgramSerieVideo} element. Her vises sig en svaghed ved XML som ikke har nogle begrænsninger på dataindholdet (fx unik) og fremmenøgle referencer mellem elementerne. Faktisk er der ingen information i de to XML dokumenter eller deres XML skemaer, som dokumenterer denne reference imellem elementerne i de to dokumenter.
Til søgning efter alle programserier i en specifik kategori eksisterer der allerede en funktion som hedder programSeriesOfCategory() som er vist i figur \ref{xquerySearch:programSeriesOfCategory}. Denne funktion blev kaldt i funktionen videosOfCategory() i figur \ref{xquerySearch:videosOfCategory}. Den resulterende sekvens indeholder alle fundende \ele{programserie} elementer, som igen har det søgte tag \ele{NewestVideoId}. En ”for lykke” behandler elementerne \ele{programserie} og et XPath udtryk selekterer kun det unik video ID, som blev gemt i variabelen \$videoId. I den samme ”for lykke” bliver \$videoId brugt som et argument i en anden XPath søgning, men denne gang i videodokumentet  all.xml. Resultatet af funktionens søgningen er elemeter af \ele{programSerieVideo} med tilhørende detaljeret informationer.


\begin{figure}[ht]
%\centering
\begin{lstlisting}[style=FAKE_XQUERY, language=XQUERY]

declare function dr:videosOfCategory
( $programseriesFileName as xs:string,  $programseriesVideoFileName as xs:string, $category as xs:string ) as element()*{

  let $programSeriesCategory := dr:programSeriesOfCategory($programseriesFileName, $category )
  return(
    for $videoId in $programSeriesCategory//ProgramSerie/NewestVideoId/text()
      return(
        let $programSerieVideo := doc($programseriesVideoFileName)//ProgramSerieVideo[Id = $videoId]
        return $programSerieVideo
      )
  )
};
\end{lstlisting}
\caption{Funktion til at finde nyeste videoer inden for en specifik kategori.}
\label{xquerySearch:videosOfCategory}
\end{figure}




\section{Find alle videoer inden for et specifikt tidsinterval}

Videoapplikationen skal have en søgefunktion, hvor brugeren kan finde alle de programserievideoer, hvor udsendelsestidspunktet ligger inden for et specifikt tidsinterval. XQuery funktionen er vist i figur \ref{xquerySearch:programSeriesVideoBroadcastDateInterval} og tager imod navnet af XML videofilen, start og stop tidspunktet, som argumenter til funktionen. En ”for lykke” læser tidsinformation i tagget \ele{BroadcastTime} af hvert element \ele{ProgramSerieVideo} og returnerer kun de \ele{ProgramSerieVideo} elementer, hvor udsendelsestidspunktet ligger inden for tidsintervallet. XQuery har to forskellige sammenligningsoperatorer, værdier sammenligningsoperatorer fx (le, ge) som er beregnet til at sammenligne enkelte værdier og generelle sammenligningsoperatorer fx (\textless=, =\textgreater) for at sammenligne sekvenser med flere værdier. I denne funktion blev der brugt den første variant fordi det kun er enkelte værdier som skal sammenlignes. Det er nødvendig at tage højde for at tagget \ele{BroadcastTime} kan være tomt. I denne situation vil cast fra null til xs:dateTime() giver en dynamisk fejl. Til at afhjælpe dette problem bruges der en hjælpefunktion, figur \ref{xquerySearch:getBroadCastTime} som kontrollerer om tagget er tomt og dermed returnere en dummy dato ("1900-01-01T00:00:00") ellers den fundende dato, til at opdatere den interne variabel \$broadCastTime.

\begin{figure}[ht]
%\centering
\begin{lstlisting}[style=FAKE_XQUERY, language=XQUERY]

declare function dr:programSeriesVideoBroadcastDateInterval
( $programseriesVideoFileName as xs:string, $dateStart as xs:dateTime, $dateStop as xs:dateTime) as element()*{
  for $progSerVideoElement in doc($programseriesVideoFileName)/ArrayOfProgramSerieVideo/ProgramSerieVideo
  let $broadCastTime := dr:getBroadCastTime($progSerVideoElement/BroadcastTime)
  where $broadCastTime ge $dateStart and $broadCastTime le $dateStop
  return $progSerVideoElement
}; 

\end{lstlisting}
\caption{Funktion til at finde videoer for et specifikt tidsinterval}
\label{xquerySearch:programSeriesVideoBroadcastDateInterval}
\end{figure}



\begin{figure}[ht]
%\centering
\begin{lstlisting}[style=FAKE_XQUERY, language=XQUERY]

declare function dr:getBroadCastTime
($broadCastTimeElement as element() ) as xs:dateTime{
  if( not($broadCastTimeElement/text()) ) then (
       (xs:dateTime("1900-01-01T00:00:00"))
    ) else (
      (xs:dateTime($broadCastTimeElement/text()))
    ) 
};

\end{lstlisting}
\caption{Funktion til at læse BroadcastTime}
\label{xquerySearch:getBroadCastTime}
\end{figure}





%TODO: Insert stuff about xquery search

% Description of each search and how it is solved

% Done! Find programs between specific date intervals.
% Done! Find all kinds of labels for prorgams.
% Done! Find the number of programs for each label.

% Identisk! Find all kinds of broadcasting channels.
% Find the number of programs for each channel.

% Find all videos from a particular series of programs sorted by date.



